# lxd_volume

Manages an LXD volume.

## Example Usage

Example of how to create a custom storage pool volume:
```hcl
resource "lxd_storage_pool" "pool" {
  name   = "mypool"
  driver = "zfs"
}

resource "lxd_volume" "volume" {
  name = "myvolume"
  pool = lxd_storage_pool.pool.name
}
```

Example of how to create and attach a custom storage pool volume to an instance:
```hcl
# Create storage pool.
resource "lxd_storage_pool" "pool" {
  name   = "mypool"
  driver = "zfs"
}

# Create custom volume.
resource "lxd_volume" "volume" {
  name = "myvol"
  pool = lxd_storage_pool.pool.name

  config = {
    size = "5GiB"
  }
}

# Create an instance with attached custom volume.
resource "lxd_instance" "instance" {
  name    = "myinstance"
  image   = "ubuntu:22.04"

  # Attach additional volume.
  device {
    name = "vol-01"
    type = "disk"
    properties = {
      path   = "/var/lib/docker"           # Path where volume is mounted within an instance.
      pool   = lxd_storage_pool.pool.name  # Storage pool name where volume is created.
      source = lxd_volume.volume.name      # Volume name.
    }
  }
}
```

## Argument Reference

* `name` - **Required** - Name of the storage volume.

* `pool` - **Required** - Name of storage pool to host the volume.

* `description` - *Optional* - Description of the volume.

* `type` - *Optional* - The "type" of volume. The default value is `custom`,
	which is the type to use for storage volumes attached to instances.

* `content_type` - *Optional* - Volume content type (`filesystem` or `block`)

* `config` - *Optional* - Map of key/value pairs of
	[volume config settings](https://documentation.ubuntu.com/lxd/en/latest/reference/storage_drivers/).
	Config settings vary depending on the Storage Pool used.

* `project` - *Optional* - Name of the project where the volume will be stored.

* `remote` - *Optional* - The remote in which the resource will be created. If
	not provided, the provider's default remote will be used.

* `target` - *Optional* - Specify a target node in a cluster.


## Attribute Reference

The following attributes are exported:

* `location` - Name of the node where volume was created. It could be useful with LXD in cluster mode.

## Importing

Import ID syntax: `[<remote>:][<project>]/<pool>/<name>`

* `<remote>` - *Optional* - Remote name.
* `<project>` - *Optional* - Project name.
* `<pool>` - **Required** - Storage pool name.
* `<name>` - **Required** - Volume name.

-> Currently, only volumes of type `custom` can be imported.

### Import example

Example using terraform import command:

```shell
$ terraform import lxd_volume.myvol proj/pool1/vol1
```

Example using the import block (only available in Terraform v1.5.0 and later):

```hcl
resource "lxd_volume" "myvol" {
  name    = "vol1"
  pool    = "pool1"
  project = "proj"
}

import {
  to = lxd_volume.myvol
  id = "proj/pool1/vol1"
}
```


## Notes

* Technically, an LXD volume is simply an instance or profile device of
  type `disk`.

* The volume resource `config` includes some keys that can be automatically generated by the LXD.
  If these keys are not explicitly defined by the user, they will be omitted from the Terraform
  state and treated as computed values.
    - `block.filesystem`
    - `block.mount_options`
    - `volatile.*`
